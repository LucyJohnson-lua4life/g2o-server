class AIAgressive extends AIBase {
	enemy_id = -1
	collect_target = AI_CollectNearestTarget
	weapon_mode = null

	// Consts
	attack_distance = 300
	target_distance = 1000
	chase_distance = 1000
	warn_time = 0
	// AI vars
	max_distance = 0
	warn_start = 0

	function Reset() {
		base.Reset()
		this.wait_until = 0
		this.warn_start = 0
		this.enemy_id = -1
	}

	function ValidateEnemy() {
		if (this.enemy_id != -1) {
			if (!isPlayerConnected(this.enemy_id) || isPlayerDead(this.enemy_id)) {
				return false
			}

			local distance = AI_GetDistancePlayers(this.id, this.enemy_id)
			//print("Distance to enemy: " + distance)

			return distance <= this.max_distance
		}

		return false
	}

	function Update(ts) {
		if (isPlayerDead(this.id)) {
			return
		}

		if (("goto_is_active" in this.flags && this.flags.goto_is_active)) {
			if (AI_Goto(this, this.flags.goto_target_name)) {
				return
			}
		}

		if (!this.ValidateEnemy() && this.collect_target) {
			local last_enemy_id = this.enemy_id
			this.enemy_id = this.collect_target(this)

			if (last_enemy_id != this.enemy_id) {
				this.OnFocusChange(last_enemy_id, this.enemy_id)
			}
		}



		if (this.enemy_id != -1) {
			this.AttackRoutine(ts)
		} else {
			this.DailyRoutine(ts)
		}
	}

	function AttackRoutine(ts) {
		// Triggered when NPC has a valid enemy target
	}

	function DailyRoutine(ts) {
		// Triggered when NPC has no enemy
	}

	function OnFocusChange(from, to) {
		// Triggered when focused enemy has changed
	}
}